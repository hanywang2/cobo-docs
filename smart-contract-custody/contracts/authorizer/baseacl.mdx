---
title: "BaseACL Example"
icon: 
description: 
---


To implement access controls for a transaction using `BaseAuthorizer,` developers will typically use the `abi.decode` function to manually decode the transaction data.

To help developers streamline the process of decoding transaction data, `Cobo Safe` has implemented `BaseACL` based upon `BaseAuthorizer`. This implementation allows developers to define a function declaration as the function of the target contract they intend to control.

`BaseACL` only implements the `preExecCheck` function because most of the access control checks are done before a transaction is executed. Developers can use `BaseACL` to configure customized access controls at both the address and function levels.

The process of using `BaseACL` to implement a customized `Authorizer` is as follows: 

1. Configure variables such as `NAME` and `VERSION.`
2. Implement the `contracts()` function. A list of smart contract addresses controlled by the  `Authorizer` will be returned. The `Authorizer` will continue the review process only if the â€œto address" of the transaction is in the list. Otherwise, the `Authorizer` will directly reject the transaction.
3. Implement functions that are used to conduct access control checks for the target contracts. The functions that are used to conduct access control checks for the target contracts should be exactly the same as the function declarations. However, they do not require return values and should not be decorated as `payable`. Additionally, they should be converted to `external view` (e.g., modifying the contracts state is not allowed).
4. When the check functions are called, their parameters should be exactly the same as those of the smart contract call. Thus, you can check the parameters of the check functions directly by using `Solidity's` `require()` statement. If the check fails, the `Authorizer` will reject the transaction.
5. If the `Authorizer` needs to manage multiple contracts, a `modifier` named `onlyContract` should be used in the access control check functions to review the contract address of the transaction, so as to prevent the `Delegate` from calling other contracts that support the same function.

The following example uses `BaseACL` to implement a customized `Authorizer`. The `Authorizer` allows `Delegate` to complete yield farming on PancakeSwap.

* `Delegate` is allowed to call the `approve()` function of LP Token but only `MasterChef` is allowed
* `Delegate` is allowed to call the `deposit()` function of MasterChef; `pid` parameter value is 3
* `Delegate` is allowed to call the `withdraw()` function of MasterChef;  `pid` parameter value is 3

```
// SPDX-License-Identifier: LGPL-3.0-only
pragma solidity ^0.8.19;

import "../base/BaseACL.sol";

contract SampleFarmACL is BaseACL {
    bytes32 public constant NAME = "SampleFarmACL";
    uint256 public constant VERSION = 1;

    address public constant LP_TOKEN = 0x58F876857a02D6762E0101bb5C46A8c1ED44Dc16;
    address public constant MASTER_CHEF = 0xa5f8C5Dbd5F286960b9d90548680aE5ebFf07652;

    constructor(address _owner, address _caller) BaseACL(_owner, _caller) {}

    // Configure a list of smart contracts controlled by Authorizer; Transaction associated with an address not in this list will be rejected by Authorizer
    function contracts() public view override returns (address[] memory _contracts) {
        _contracts = new address[](2);
        _contracts[0] = LP_TOKEN;
        _contracts[1] = MASTER_CHEF;
    }

    // Review approve() function call for LP Token; only MasterChef contract is authorized
    function approve(address spender, uint256 amount) 
        external view 
        onlyContract(LP_TOKEN)
    {
        require(spender == MASTER_CHEF, "approve: Invalid spender");
    }

    // Review deposit() function call; pid parameter value is 3
    function deposit(uint256 _pid, uint256 _amount) 
        external view 
        onlyContract(MASTER_CHEF)
    {
        require(_pid == 3, "deposit: Pool is not allowed");
    }

    // Review withdraw() funtion call; pid parameter value is 3
    function withdraw(uint256 _pid, uint256 _amount) 
        external view 
        onlyContract(MASTER_CHEF)
    {
        require(_pid == 3, "withdraw: Pool is not allowed");
    }
}
```
